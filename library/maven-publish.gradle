group = 'io.nyris'
version = gitVersionName

afterEvaluate {
    publishing {
        repositories {
            maven {
                credentials {
                    username System.getenv("NUGET_USER")
                    password System.getenv("NUGET_PASSWORD")
                }
                url System.getenv("NUGET_URL")
            }
            maven {
                name = 'LocalMaven'
                url = "${project.buildDir}/maven"
            }
        }

        publications {
            release(MavenPublication) {
                from components.findByName('release')

                groupId = POM_GROUP_ID
                artifactId = POM_ARTIFACT_ID
                version = gitVersionName

                pom {
                    url = POM_URL
                    name = POM_NAME
                    description = POM_DESCRIPTION
                    inceptionYear = '2018'
                    packaging = 'aar'
                    groupId = POM_GROUP_ID
                    artifactId = POM_ARTIFACT_ID

                    organization {
                        name = POM_ORGANIZATION_NAME
                        url = POM_URL
                    }

                    licenses {
                        license {
                            name = 'APACHE LICENSE, VERSION 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = POM_DEVELOPER_ID
                            name = POM_DEVELOPER_NAME
                        }
                    }

                    withXml {
                        def pomNode = asNode()
                        pomNode.dependencies.'*'.findAll() {
                            it.scope.text() == 'test'
                        }.each() {
                            it.parent().remove(it)
                        }
                    }
                }
            }
        }
    }

}
